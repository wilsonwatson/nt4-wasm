import groovy.json.JsonSlurper;

plugins {
    id 'java-library'
    id "edu.wpi.first.GradleRIO" version "2023.4.3"
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

def version_major = 0
def version_minor = 3
def version_patch = 0

def user = "wilsonwatson"
def group = "org." + user
def artifact = "nt4wasm"
def version_str = version_major + "." + version_minor + "." + version_patch

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

dependencies {
    implementation wpi.java.deps.wpilib()
    // implementation wpi.java.vendor.java()

    implementation "io.javalin:javalin:5.6.1"
}

task buildWasm(type: Exec) {
    onlyIf {
        try {
            def result = exec {
                ignoreExitValue = true
                commandLine 'wasm-pack', '--version'
            }
            return true
        } catch(Exception e) {
            return false
        }
    }

    workingDir '.'
    commandLine 'wasm-pack', 'build', '--target', 'web'
}

tasks.jar.dependsOn(buildWasm)

jar {
    from('pkg') {
        include('nt4_wasm_bg.wasm')
        include('nt4_wasm.js')
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

println "building \"" + group + ":" + artifact + ":" + version_str + "\""

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = artifact
            version = version_str

            from components.java
        }
    }

    repositories {
        maven {
            name = "GithubPackages"
            url = "https://maven.pkg.github.com/wilsonwatson/nt4-wasm"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
